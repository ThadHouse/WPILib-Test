apply plugin: 'cpp'
apply plugin: 'google-test'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.native-utils'
apply plugin: 'edu.wpi.first.exports-utils'
apply plugin: 'edu.wpi.first.dependency-utils'
apply plugin: 'edu.wpi.first.publish-utils'

apply from: '../config.gradle'

model {
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = '+'
            sharedConfigs = [ wpilibc: [],
                              wpilibcTestingBase: [] ]
        }
        ntcore(DependencyConfig) {
            groupId = 'edu.wpi.first.ntcore'
            artifactId = 'ntcore-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = '+'
            sharedConfigs = [ wpilibc: [],
                              wpilibcTestingBase: [] ]
        }
    }
}

model {
    exportsConfigs {
        wpilibc(ExportsConfig) {
            x86ExcludeSymbols = [ '_CT??_R0?AV_System_error', '_CT??_R0?AVexception', '_CT??_R0?AVfailure',
                                  '_CT??_R0?AVbad_cast',
                                  '_CT??_R0?AVruntime_error', '_CT??_R0?AVsystem_error', '_CTA5?AVfailure',
                                  '_TI5?AVfailure' ]
            x64ExcludeSymbols = [ '_CT??_R0?AV_System_error', '_CT??_R0?AVexception', '_CT??_R0?AVfailure',
                                  '_CT??_R0?AVbad_cast',
                                  '_CT??_R0?AVruntime_error', '_CT??_R0?AVsystem_error', '_CTA5?AVfailure',
                                  '_TI5?AVfailure' ]
        }
    }
}

model {
    components {
        wpilibc(NativeLibrarySpec) {
            binaries.all { binary->
                if (binary.targetPlatform.architecture.name == 'athena') {
                    tasks.withType(CppCompile) {
                        cppCompiler.args "-DCONFIG_ATHENA"
                    }
                    project(':ni-libraries').addNiLibrariesToLinker(binary)
                    binary.lib project: ':hal', library: 'halAthena', linkage: 'shared'
                } else {
                    binary.lib project: ':hal', library: 'halSim', linkage: 'shared'
                }
            }
        }
        wpilibcTestingBase(NativeLibrarySpec) {
        }
    }
    binaries {
        withType(GoogleTestTestSuiteBinarySpec) {
            if (it.component.testedComponent.name != 'wpilibc') {
                lib project: ':gmock', library: 'gmock', linkage: 'static'
                lib project: ':hal', library: 'halSim', linkage: 'shared'
                lib library: 'wpilibc', linkage: 'shared'
            } else {
                it.buildable = false
            }
        }
    }
}
task cppSourcesZip(type: Zip) {
    destinationDir = project.buildDir
    classifier = "sources"

    from('src/wpilibc/cpp') {
        into '/'
    }
}

task cppHeadersZip(type: Zip) {
    destinationDir = project.buildDir
    classifier = "headers"

    from('src/wpilibc/headers') {
        into '/'
    }
}

apply from: 'publish.gradle'